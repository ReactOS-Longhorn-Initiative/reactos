
spec2def(ucrtbase.dll ucrtbase.spec ADD_IMPORTLIB)

add_library(ucrtbase SHARED
    stubs.c
    ucrtbase_stubs.c
    ucrtbase.def
    ${REACTOS_SOURCE_DIR}/sdk/lib/crt/startup/tlsmcrt.c # HACK
    ${REACTOS_SOURCE_DIR}/sdk/lib/crt/startup/tlsmthread.c
    ${REACTOS_SOURCE_DIR}/sdk/lib/crt/startup/tlsthrd.c
)

if(MSVC)
    # Disable default libraries
    target_link_options(ucrtbase PRIVATE "/NODEFAULTLIB:MSVCRTD")

    # Set entrypoint
    target_link_options(ucrtbase PRIVATE "/ENTRY:__acrt_DllMain")
else()
    target_compile_options(ucrtbase PRIVATE -Wno-builtin-declaration-mismatch)
endif()

if(CMAKE_C_COMPILER_ID STREQUAL "Clang")
    # Silence warnings in ucrtbase_stubs.c
    target_compile_options(ucrtbase PRIVATE -Wno-incompatible-library-redeclaration)
endif()

set_entrypoint(ucrtbase __acrt_DllMain 12)

target_link_libraries(ucrtbase
    ucrt
    ucrtsupport
    wine
)

if(MSVC)
    target_link_libraries(ucrtbase runtmchk)
else()
    # For __cxa_guard_acquire / __cxa_guard_release
    target_link_libraries(ucrtbase libsupc++)
endif()

add_importlibs(ucrtbase kernel32 ntdll)

add_cd_file(TARGET ucrtbase DESTINATION reactos/system32 FOR all)
